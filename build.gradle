description = "demo application"

apply plugin: "war"
apply plugin: "java"
apply plugin: "jetty"

sourceCompatibility = "1.5"
targetCompatibility = "1.5"

group = "de.threeoldcoders.web.tapestry.hibernate"
version = "0.1-SNAPSHOT"

tasks.withType(Compile) {
  options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    // All things JBoss/Hibernate
    mavenRepo name: "JBoss", url: "http://repository.jboss.org/nexus/content/groups/public/"

    // For stable versions of the tapx libraries
    mavenRepo name: "HLS", url: "http://howardlewisship.com/repository/"

    // For non-stable versions of the tapx libraries
    mavenRepo name: "HLS Snapshots", url: "http://howardlewisship.com/snapshot-repository/"

    // For access to Apache Staging (Preview) packages
    mavenRepo name: "Apache Staging", url: "https://repository.apache.org/content/groups/staging"
}

// This simulates Maven's "provided" scope, until it is officially supported by Gradle
// See http://jira.codehaus.org/browse/GRADLE-784

configurations {
    provided
    jettyRuntimeJars
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

dependencies {

    compile "org.apache.tapestry:tapestry-core:5.3.7"
    compile "org.apache.tapestry:tapestry-hibernate-core:5.3.7"
    compile "org.apache.tapestry:tapestry-hibernate:5.3.7"
    compile "org.hibernate:hibernate-commons-annotations:3.2.0.Final"
    compile "org.hibernate:hibernate-core:3.6.10.Final"
    compile "org.hibernate:hibernate-validator:3.1.0.GA"

    // This adds automatic compression of JavaScript and CSS in production mode:
    // compile "org.apache.tapestry:tapestry-yuicompressor:5.3.7"

    // Uncomment this to add support for file uploads:
    // compile "org.apache.tapestry:tapestry-upload:5.3.7"

    provided "javax.servlet:servlet-api:2.5"
    provided "org.projectlombok:lombok:0.11.6"

    // for jettyRun, jettyRunWar
    jettyRuntimeJars "org.hsqldb:hsqldb:2.2.9"
}

test {
    useTestNG()

    options.suites("src/test/conf/testng.xml")

    systemProperties["tapestry.service-reloading-enabled"] = "false"
    systemProperties["tapestry.execution-mode"] = "development"

    maxHeapSize = "600M"

    jvmArgs("-XX:MaxPermSize=256M")

    enableAssertions = true
}

//
// Configure jettyRun, jettyRunWar and jettyStop tasks.
// 
[jettyRun, jettyRunWar]*.with {
  additionalRuntimeJars = configurations.jettyRuntimeJars
  jettyConfig = file('src/test/resources/jetty-env.xml')
  contextPath = 'demo'
  httpPort = 8080
}

